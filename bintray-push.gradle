File local = rootProject.file('local.properties')
if(!local.exists()) {
    return
}

apply plugin: 'com.github.dcendents.android-maven'

ext {
    gitUrl = 'https://github.com/seroperson/itstimetoact.git'
    siteUrl = 'https://github.com/seroperson/itstimetoact'
}

install.repositories.mavenInstaller.pom.project {
    packaging 'aar'
    groupId project.group
    artifactId libraryName

    name libraryName
    description libraryDescription
    version project.version
    url siteUrl

    licenses {
        license {
            name 'The MIT License'
            url 'https://opensource.org/licenses/MIT'
        }
    }
    developers {
        developer {
            id 'seroperson'
            name 'Daniil Sivak'
            email 'seroperson@gmail.com'
        }
    }
}

apply plugin: 'com.jfrog.bintray'


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(local.newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = libraryName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish false
        publicDownloadNumbers = true
    }
}
